% how to prove the output of some relation is unique

i : type.
z : i.
s : i -> i.

add : i -> i -> i -> type.
%mode add +M +N -P.
add/z : add z N N.
add/s : add (s M) N (s P) <- add M N P.
%worlds () (add M _ _).
%total (M) (add M _ _).

id : i -> i -> type.
id/refl : id N N.

id/cong : id M N -> id (s M) (s N) -> type.
%mode id/cong +E1 -E2.
- : id/cong id/refl id/refl.
%worlds () (id/cong _ _).
%total {} (id/cong _ _).

unique/add : add M N P1 -> add M N P2 -> id P1 P2 -> type.
%mode unique/add +D1 +D2 -E.
- : unique/add add/z add/z id/refl.
- : unique/add (add/s D1 : add (s M) N (s P1))
               (add/s D2 : add (s M) N (s P2)) Eout
    <- unique/add D1 D2 E
    <- id/cong E Eout.
%worlds () (unique/add _ _ _).
%total D (unique/add D _ _).

