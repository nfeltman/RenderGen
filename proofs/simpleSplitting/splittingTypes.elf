%% TYPE SPLITTING

tysplit : ty -> ty -> ty -> type.

spl-unit : tysplit unit unit unit.
spl-fut  : tysplit (fut A) unit A.
spl-prod : tysplit (prod A B) (prod A1 B1) (prod A2 B2)
	<- tysplit B B1 B2
	<- tysplit A A1 A2.
spl-arr  : tysplit (arr+ A T B) (arr A1 (prod B1 T)) (arr (prod A2 T) B2)
	<- tysplit B B1 B2
	<- tysplit A A1 A2.

tysplit-pres : tysplit A A1 A2 -> ty+wf A wor1 -> ty-wf A1 -> ty-wf A2 -> type.
%mode tysplit-pres +Dspl +Dwf -Dwf1 -Dwf2.
- : tysplit-pres spl-unit ty+wf-unit ty-wf-unit ty-wf-unit.
- : tysplit-pres (spl-prod DsplA DsplB) (ty+wf-prod DwfA DwfB) 
		(ty-wf-prod DwfA1 DwfB1) (ty-wf-prod DwfA2 DwfB2)
	<- tysplit-pres DsplA DwfA DwfA1 DwfA2
	<- tysplit-pres DsplB DwfB DwfB1 DwfB2.
- : tysplit-pres spl-fut (ty+wf-fut Dwf) ty-wf-unit Dwf'
	<- wor2+mono Dwf Dwf'.
- : tysplit-pres (spl-arr DsplA DsplB) (ty+wf-arr+ DwfA DwfT DwfB) 
		(ty-wf-arr DwfA1 (ty-wf-prod DwfB1 DwfT)) (ty-wf-arr (ty-wf-prod DwfA2 DwfT) DwfB2)
	<- tysplit-pres DsplA DwfA DwfA1 DwfA2
	<- tysplit-pres DsplB DwfB DwfB1 DwfB2.
%worlds () (tysplit-pres _ _ _ _).
%total (D) (tysplit-pres D _ _ _).

tysplit-tot : ty+wf A wor1 -> tysplit A _ _ -> type.
%mode tysplit-tot +Dwf -Dspl.
- : tysplit-tot ty+wf-unit spl-unit.
- : tysplit-tot (ty+wf-prod DwfA DwfB) (spl-prod DsplA DsplB)
	<- tysplit-tot DwfA DsplA
	<- tysplit-tot DwfB DsplB.
- : tysplit-tot (ty+wf-arr+ DwfA _ DwfB) (spl-arr DsplA DsplB)
	<- tysplit-tot DwfA DsplA
	<- tysplit-tot DwfB DsplB.
- : tysplit-tot (ty+wf-fut _) spl-fut.
%worlds () (tysplit-tot _ _).
%total (D) (tysplit-tot D _).