%{ monostage.elf }%

%% VALUE
val : term -> type. %name val Dval.
val-ztup	: val ztup.
val-tup	: val (tup E1 E2)
				<- val E2
				<- val E1.
val-lam	: val (lam _ _).


%% STEP
mstep : term -> term -> type.

ms-tup-s1	: mstep (tup E1 E2) (tup E1' E2)
			<- mstep E1 E1'.
ms-tup-s2	: mstep (tup E1 E2) (tup E1 E2')
			<- val E1
			<- mstep E2 E2'.
ms-pi1-s	: mstep (pi1 E) (pi1 E')
			<- mstep E E'.
ms-pi1-r	: mstep (pi1 (tup E1 E2)) E1
			<- val (tup E1 E2).
ms-pi2-s	: mstep (pi2 E) (pi2 E')
			<- mstep E E'.
ms-pi2-r	: mstep (pi2 (tup E1 E2)) E2
			<- val (tup E1 E2).
ms-app-s1	: mstep (app E1 E2) (app E1' E2)
			<- mstep E1 E1'.
ms-app-s2	: mstep (app E1 E2) (app E1 E2')
			<- val E1
			<- mstep E2 E2'.
ms-app-r	: mstep (app (lam _ M) E) (M E)
			<- val E.
ms-let-s	: mstep (let E M) (let E' M)
			<- mstep E E'.
ms-let-r	: mstep (let E M) (M E)
			<- val E.
ms-lett-s	: mstep (lett E M) (lett E' M)
			<- mstep E E'.
ms-lett-r	: mstep (lett (tup E1 E2) M) (M E1 E2)
			<- val (tup E1 E2).
			
%% PRESERVATION
pres : mstep E E' -> of E T worM -> of E' T worM -> type.
%mode pres +Dstep +Dof -Dof'.
- : pres (ms-tup-s1 Dstep) (of-tup Dof1 Dof2) (of-tup Dof1' Dof2)
	<- pres Dstep Dof1 Dof1'.
- : pres (ms-tup-s2 Dstep _) (of-tup Dof1 Dof2) (of-tup Dof1 Dof2')
	<- pres Dstep Dof2 Dof2'. 
- : pres (ms-pi1-s Dstep) (of-pi1 Dof) (of-pi1 Dof')
	<- pres Dstep Dof Dof'.
- : pres (ms-pi1-r _) (of-pi1 (of-tup Dof _)) Dof.
- : pres (ms-pi2-s Dstep) (of-pi2 Dof) (of-pi2 Dof')
	<- pres Dstep Dof Dof'.
- : pres (ms-pi2-r _) (of-pi2 (of-tup _ Dof)) Dof.
- : pres (ms-app-s1 Dstep) (of-app Dof1 Dof2) (of-app Dof1' Dof2)
	<- pres Dstep Dof1 Dof1'.
- : pres (ms-app-s2 Dstep _) (of-app Dof1 Dof2) (of-app Dof1 Dof2')
	<- pres Dstep Dof2 Dof2'.
- : pres (ms-app-r (_ : val E)) (of-app (of-lam _ DofM) DofE) (DofM E DofE).
- : pres (ms-let-s Dstep) (of-let DofM DofE) (of-let DofM DofE') 
	<- pres Dstep DofE DofE'.
- : pres (ms-let-r (_ : val E)) (of-let DofM DofE) (DofM E DofE).
%worlds () (pres _ _ _).
%total (Dstep) (pres Dstep _ _).

%% VALUE-OR-STEP JUDGEMENT
vos : term -> type.
vos-val : val E -> vos E.
vos-step : mstep E E' -> vos E.

%% PROGRESS LEMMAS
prog/tup : vos E1 -> vos E2 -> vos (tup E1 E2) -> type.
%mode prog/tup +Dvos1 +Dvos2 -Dvos.
- : prog/tup (vos-step Dstep) _ (vos-step (ms-tup-s1 Dstep)).
- : prog/tup (vos-val Dval) (vos-step Dstep) (vos-step (ms-tup-s2 Dstep Dval)).
- : prog/tup (vos-val Dval1) (vos-val Dval2) (vos-val (val-tup Dval1 Dval2)).
%worlds () (prog/tup _ _ _).
%total (Dvos) (prog/tup Dvos _ _).

prog/pi1 : vos E -> of E (prod T1 T2) worM -> vos (pi1 E) -> type.
%mode prog/pi1 +Dvos +Dof -Dvos'.
- : prog/pi1 (vos-step Dstep) _ (vos-step (ms-pi1-s Dstep)).
- : prog/pi1 (vos-val Dval) _ (vos-step (ms-pi1-r Dval)).
%worlds () (prog/pi1 _ _ _).
%total (Dvos) (prog/pi1 Dvos _ _).

prog/pi2 : vos E -> of E (prod T1 T2) worM -> vos (pi2 E) -> type.
%mode prog/pi2 +Dvos +Dof -Dvos'.
- : prog/pi2 (vos-step Dstep) _ (vos-step (ms-pi2-s Dstep)).
- : prog/pi2 (vos-val Dval) _ (vos-step (ms-pi2-r Dval)).
%worlds () (prog/pi2 _ _ _).
%total (Dvos) (prog/pi2 Dvos _ _).

prog/app : vos E1 -> vos E2 -> of E1 (arr T1 T2) worM -> vos (app E1 E2) -> type.
%mode prog/app +Dvos1 +Dvos2 +Dof -Dvos.
- : prog/app (vos-step Dstep) _ _ (vos-step (ms-app-s1 Dstep)).
- : prog/app (vos-val Dval) (vos-step Dstep) _ (vos-step (ms-app-s2 Dstep Dval)).
- : prog/app (vos-val _) (vos-val Dval) _ (vos-step (ms-app-r Dval)).
%worlds () (prog/app _ _ _ _).
%total (Dvos) (prog/app Dvos _ _ _).

prog/let : vos E -> of (let E M) T worM -> vos (let E M) -> type.
%mode prog/let +Dvos +Dof -Dvos'.
- : prog/let (vos-step Dstep) _ (vos-step (ms-let-s Dstep)).
- : prog/let (vos-val Dval) _ (vos-step (ms-let-r Dval)).
%worlds () (prog/let _ _ _).
%total (Dvos) (prog/let Dvos _ _).

%% PROGRESS
prog : of E T worM -> vos E -> type.
%mode prog +Dof -Dvos.
- : prog _ (vos-val val-ztup).
- : prog (of-tup Dof1 Dof2) Dvos
	<- prog Dof1 Dvos1
	<- prog Dof2 Dvos2
	<- prog/tup Dvos1 Dvos2 Dvos.
- : prog (of-pi1 Dof) Dvos'
	<- prog Dof Dvos
	<- prog/pi1 Dvos Dof Dvos'.
- : prog (of-pi2 Dof) Dvos'
	<- prog Dof Dvos
	<- prog/pi2 Dvos Dof Dvos'.
- : prog _ (vos-val val-lam).
- : prog (of-app Dof1 Dof2) Dvos
	<- prog Dof1 Dvos1
	<- prog Dof2 Dvos2
	<- prog/app Dvos1 Dvos2 Dof1 Dvos.
- : prog (of-let DofM DofE) Dvos'
	<- prog DofE Dvos
	<- prog/let Dvos (of-let DofM DofE) Dvos'.
%worlds () (prog _ _).
%total (Dvos) (prog Dvos _).
