
%% SOME NECESSARY STRENGTHENING LEMMAS

%block strengthening-block = (term-block | of^-form^-block | any-split).


strengthen-splitGv : 
	({x : term lang0} splitGv x Q0 -> splitG (E x) (Q x)) -> 
	({x : term lang0} splitGv x Q0 -> splitG (E x) Q') -> type.
%mode strengthen-splitGv +Dspl -Dspl'.
- : strengthen-splitGv ([x][s] Dsplr x s : splitG (E x) (Q x)) Dsplr'
	<- lang-indep neq-lang_01 Q Deq
	<- ({x}{s} splitG-resp (Dsplr x s) eq-term_ (Deq x) (Dsplr' x s)).
%worlds (strengthening-block) (strengthen-splitGv _ _).
%total (D) (strengthen-splitGv D _).


strengthen-split1 : 
	({x : term lang0}{x1 : term lang1}{x2 : term lang2} splitval x x1 x2 -> split1 (E x) (P x x1 x2) (Q x x1 x2)) -> 
	({x : term lang0}{x1 : term lang1}{x2 : term lang2} splitval x x1 x2 -> split1 (E x) (P' x1) (Q' x2)) -> type.
%mode strengthen-split1 +Dspl -Dspl'.
- : strengthen-split1 ([x][x1][x2][s] Dspl x x1 x2 s : split1 (E x) (P x x1 x2) (Q x x1 x2)) Dspl''
	<- ({x1}{x2} lang-indep neq-lang_01 ([x] P x x1 x2) ([x] DeqP x x1 x2))
	<- ({l}{x1}{x2} lang-indep neq-lang_02 ([x] Q x x1 x2 l) ([x] DeqQ x x1 x2 l))
	<- ({x}{x1}{x2}{s} split1-resp (Dspl x x1 x2 s) eq-term_ (DeqP x x1 x2) (DeqQ x x1 x2) (Dspl' x x1 x2 s : split1 (E x) (P' x1 x2) (Q' x1 x2)))
	<- ({x1} lang-indep neq-lang_21 ([x2] P' x1 x2) ([x2] DeqP' x1 x2))
	<- ({l}{x2} lang-indep neq-lang_12 ([x1] Q' x1 x2 l) ([x1] DeqQ' x1 x2 l))
	<- ({x}{x1}{x2}{s} split1-resp (Dspl' x x1 x2 s) eq-term_ (DeqP' x1 x2) ([l] DeqQ' x1 x2 l) (Dspl'' x x1 x2 s)).
%worlds (strengthening-block) (strengthen-split1 _ _).
%total (D) (strengthen-split1 D _).

%% SPLITTING GROUND TERMS, VALUES, AND RESIDUALS IS TOTAL (DEFINITIONS)
splvar2-total : of^ E A wor2 -> form^ E vars2 wor2 -> splitvar2 E _ -> type.
%mode splvar2-total +Dof +Dvar -Dspl.
splgv-total : of^ E A worG -> form^ E norm1 worG -> splitGv E _ -> type.
%mode splgv-total +Dof +Dvar -Dspl.
splv-total : of^ E A wor1 -> form^ E norm1 wor1 -> splitval E _ _ -> type.
%mode splv-total +Dof +Dvar -Dspl.

%% BLOCKS
%block total-varG : some {A : ty} block 
	{x:term lang0} {x1:term lang1} 
	{dof : of^ x A worG} {vx : form^ x norm1 worG}
	{dspl : splitGv x x1}
	{dtotv : splgv-total dof vx dspl}.
%block total-var1 : some {A : ty} block 
	{x:term lang0} {x1:term lang1} {x2:term lang2} 
	{dof : of^ x A wor1} {vx : form^ x norm1 wor1}
	{dspl : splitval x x1 x2}
	{dtotv : splv-total dof vx dspl}.
%block total-var2 : some {A : ty} block 
	{x:term lang0} {x2:term lang2} 
	{dof : of^ x A wor2} {vx : form^ x vars2 wor2}
	{dsplr : splitvar2 x x2}
	{_ : splvar2-total dof vx dsplr}.
%block total-block = (total-varG | total-var1 | total-var2).

%% SPLITTING VARIABLES IT TOTAL (TRIVIAL, BECAUSE ALWAYS ASSUMED)
%worlds (total-block) (splvar2-total _ _ _).
%total {} (splvar2-total _ _ _).

%% SPLITTING RESIDUALS IS TOTAL (PROOF)
splr-total : of^ E A wor2 -> form^ E norm1 wor2 -> splitres E _ -> type.
%mode splr-total +Dof +Dres -Dspl.
splr-total/branch : ({x} of^ x B wor2 -> of^ (E x) A wor2) -> ({x} form^ x vars2 wor2 -> form^ (E x) norm1 wor2) -> ({x}{x2} splitvar2 x x2 -> splitres (E x) (E2 x2)) -> type.
%mode splr-total/branch +Dof +Dres -Dspl.

- : splr-total/branch Dof Dres Dsplr'
	<- ({x} {x2} {dof} {vx} {dspl : splitvar2 x x2} splvar2-total dof vx dspl ->
		splr-total (Dof x dof) (Dres x vx) (Dsplr x x2 dspl : splitres (E x) (Q x x2)))
	<- ({x2} lang-indep neq-lang_02 ([x] Q x x2) ([x] Deq x x2))
	<- ({x}{x2}{s} splitres-resp (Dsplr x x2 s) eq-term_ (Deq x x2) (Dsplr' x x2 s)).

- : splr-total (of^-exv Dof) (form^var2 Dvar) (splr-var Dspl)
	<- splvar2-total Dof Dvar Dspl.
- : splr-total of^-ztup _ splr-ztup.
- : splr-total (of^-tup Dof1 Dof2) (form^tup-n Dres1 Dres2) (splr-tup Dsplr1 Dsplr2)
	<- splr-total Dof1 Dres1 Dsplr1
	<- splr-total Dof2 Dres2 Dsplr2.
- : splr-total (of^-pi1 Dof) (form^pi1-n2 Dres) (splr-pi1 Dsplr)
	<- splr-total Dof Dres Dsplr.
- : splr-total (of^-pi2 Dof) (form^pi2-n2 Dres) (splr-pi2 Dsplr)
	<- splr-total Dof Dres Dsplr.
- : splr-total (of^-inj1 _ Dof _) (form^inj1-n Dres _) (splr-inj1 Dsplr)
	<- splr-total Dof Dres Dsplr.
- : splr-total (of^-inj2 _ Dof _) (form^inj2-n Dres _) (splr-inj2 Dsplr)
	<- splr-total Dof Dres Dsplr.
- : splr-total (of^-roll _ Dof) (form^roll-n Dres) (splr-roll Dsplr)
	<- splr-total Dof Dres Dsplr.
- : splr-total (of^-unrl Dof) (form^unrl-n2 Dres) (splr-unrl Dsplr)
	<- splr-total Dof Dres Dsplr.
- : splr-total (of^-case DofE DofM DofN _) (form^case-n2 DresE DresM DresN) (splr-case DsplrE DsplrM DsplrN)
	<- splr-total DofE DresE DsplrE
	<- splr-total/branch DofM DresM DsplrM
	<- splr-total/branch DofN DresN DsplrN.
- : splr-total (of^-lam _ Dof) (form^lam2n Dres) (splr-lam Dsplr)
	<- splr-total/branch Dof Dres Dsplr.
- : splr-total (of^-app Dof1 Dof2) (form^app-n2 Dres1 Dres2) (splr-app Dsplr1 Dsplr2)
	<- splr-total Dof1 Dres1 Dsplr1
	<- splr-total Dof2 Dres2 Dsplr2.
- : splr-total (of^-let DofE DofM) (form^let-n2 DresE DresM) (splr-let DsplrE DsplrM)
	<- splr-total DofE DresE DsplrE
	<- splr-total/branch DofM DresM DsplrM.
%worlds (total-block) (splr-total _ _ _) (splr-total/branch _ _ _).
%total (D1 D2) (splr-total D1 _ _) (splr-total/branch D2 _ _).

%% SPLITTING GROUND TERMS AND VALUES
splg-total : of^ E A worG -> form^ E comp1 worG -> splitG E _ -> type.
%mode splg-total +Dof +Dcomp -Dspl.
splg-total/branch : 
	({x} of^ x B worG -> of^ (E x) A worG) -> 
	({x} form^ x norm1 worG -> form^ (E x) comp1 worG) -> 
	({x} {x1} splitGv x x1 -> splitG (E x) (G x1)) -> type.
%mode splg-total/branch +Dof +Dcomp -Dspl.
- : splg-total/branch Dof Dcomp Dsplg'
	<- ({x}{x1} {dof} {vx} {dspl : splitGv x x1} splgv-total dof vx dspl ->
		splg-total (Dof x dof) (Dcomp x vx) (Dsplg x x1 dspl))
	<- ({x1} strengthen-splitGv ([x][dspl] Dsplg x x1 dspl) ([x][dspl] Dsplg' x x1 dspl)).

- : splgv-total of^-ztup _ splgv-ztup.
- : splgv-total (of^-tup Dof1 Dof2) (form^tup-n Dval1 Dval2) (splgv-tup Dsplg1 Dsplg2)
	<- splgv-total Dof1 Dval1 Dsplg1
	<- splgv-total Dof2 Dval2 Dsplg2.
- : splgv-total (of^-inj1 _ Dof _) (form^inj1-n Dcomp _) (splgv-inj1 Dsplg)
	<- splgv-total Dof Dcomp Dsplg.
- : splgv-total (of^-inj2 _ Dof _) (form^inj2-n Dcomp _) (splgv-inj2 Dsplg)
	<- splgv-total Dof Dcomp Dsplg.
- : splgv-total (of^-roll _ Dof) (form^roll-n Dcomp) (splgv-roll Dsplg)
	<- splgv-total Dof Dcomp Dsplg.
- : splgv-total (of^-lam _ Dof) (form^lam1 Dcomp _) (splgv-lam Dsplg)
	<- splg-total/branch Dof Dcomp Dsplg.

- : splg-total (of^-tup Dof1 Dof2) (form^tup-c Dcomp1 Dcomp2) (splg-tup Dsplg1 Dsplg2)
	<- splg-total Dof1 Dcomp1 Dsplg1
	<- splg-total Dof2 Dcomp2 Dsplg2.
- : splg-total (of^-pi1 Dof) (form^pi1-c Dcomp) (splg-pi1 Dsplg)
	<- splg-total Dof Dcomp Dsplg.
- : splg-total (of^-pi2 Dof) (form^pi2-c Dcomp) (splg-pi2 Dsplg)
	<- splg-total Dof Dcomp Dsplg.
- : splg-total (of^-inj1 _ Dof _) (form^inj1-c Dcomp _) (splg-inj1 Dsplg)
	<- splg-total Dof Dcomp Dsplg.
- : splg-total (of^-inj2 _ Dof _) (form^inj2-c Dcomp _) (splg-inj2 Dsplg)
	<- splg-total Dof Dcomp Dsplg.
- : splg-total (of^-roll _ Dof) (form^roll-c Dcomp) (splg-roll Dsplg)
	<- splg-total Dof Dcomp Dsplg.
- : splg-total (of^-unrl Dof) (form^unrl-c Dcomp) (splg-unrl Dsplg)
	<- splg-total Dof Dcomp Dsplg.
- : splg-total (of^-case DofE DofM DofN _) (form^case-cG DcompE DcompM DcompN) (splg-case DsplgE DsplgM DsplgN)
	<- splg-total DofE DcompE DsplgE
	<- splg-total/branch DofM DcompM DsplgM
	<- splg-total/branch DofN DcompN DsplgN.
- : splg-total (of^-app Dof1 Dof2) (form^app-c Dcomp1 Dcomp2) (splg-app Dsplg1 Dsplg2)
	<- splg-total Dof1 Dcomp1 Dsplg1
	<- splg-total Dof2 Dcomp2 Dsplg2.
- : splg-total (of^-exv Dof) (form^exv Dcomp) (splg-exv Dsplg)
	<- splgv-total Dof Dcomp Dsplg.
%worlds (total-block) (splg-total _ _ _) (splgv-total _ _ _) (splg-total/branch _ _ _).
%total (Dsplg Dsplgv D3) (splg-total Dsplg _ _) (splgv-total Dsplgv _ _) (splg-total/branch D3 _ _).



%% MAIN SPLITTING TOTALITY
spl1-total : of^ E A wor1 -> form^ E comp1 wor1 -> split1 E _ _ -> type.
%mode spl1-total +Dof +Dcomp -Dspl.
spl2-total : of^ E A wor2 -> form^ E comp1 wor2 -> split2 E _ _ -> type.
%mode spl2-total +Dof +Dcomp -Dspl.
spl2-total/branch : ({x} of^ x B wor2 -> of^ (E x) A wor2) -> ({x} form^ x vars2 wor2 -> form^ (E x) comp1 wor2) -> 
	({x}{x2} splitvar2 x x2 -> split2 (E x) P (R x2)) -> type.
%mode spl2-total/branch +Dof +Dcomp -Dspl.
spl1-total/branchG : ({x} of^ x B worG -> of^ (E x) A wor1) -> ({x} form^ x norm1 worG -> form^ (E x) comp1 wor1) -> 
	({x}{x1} splitGv x x1 -> split1 (E x) (C x1) R) -> type.
%mode spl1-total/branchG +Dof +Dcomp -Dspl.

%% SPLITTING VALUES IS TOTAL
- : splv-total of^-ztup _ splv-ztup.
- : splv-total (of^-next Dof) (form^next-n Dcomp) (splv-next Dsplr)
	<- splvar2-total Dof Dcomp Dsplr.
- : splv-total (of^-roll _ Dof) (form^roll-n Dcomp) (splv-roll Dspl)
	<- splv-total Dof Dcomp Dspl.
- : splv-total (of^-tup Dof1 Dof2) (form^tup-n Dcomp1 Dcomp2) (splv-tup Dspl1 Dspl2)
	<- splv-total Dof1 Dcomp1 Dspl1
	<- splv-total Dof2 Dcomp2 Dspl2.
- : splv-total (of^-lam Dwf Dof) (form^lam1 Dcomp _) (splv-lam Dspl')
	<- ({x}{x1}{x2} {ofx} {vx} {dspl} splv-total ofx vx dspl ->
		spl1-total (Dof x ofx) (Dcomp x vx) (Dspl x x1 x2 dspl))
	<- strengthen-split1 Dspl Dspl'.
- : splv-total (of^-grnd Dof) (form^grnd Dcomp) (splv-grnd Dsplr)
	<- splgv-total Dof Dcomp Dsplr.

%% BRANCH CASES
- : spl2-total/branch Dof Dcomp Dsplr'
	<- ({x}{x2} {dof} {vx} {dspl : splitvar2 x x2} splvar2-total dof vx dspl ->
		spl2-total (Dof x dof) (Dcomp x vx) (Dsplr x x2 dspl : split2 (E x) (P x x2) (Q x x2)))
	<- lang-indep2 neq-lang_01 neq-lang_21 ([x][x2] P x x2) ([x][x2] DeqP x x2)
	<- ({l}{x2} lang-indep neq-lang_02 ([x] Q x x2 l) ([x] DeqQ x x2 l))
	<- ({x}{x2}{s} split2-resp (Dsplr x x2 s) eq-term_ (DeqP x x2) (DeqQ x x2) (Dsplr' x x2 s : split2 (E x) P' _)).

- : spl1-total/branchG Dof Dcomp Dspl'
	<- ({x}{x1} {dof} {vx} {dspl} splgv-total dof vx dspl ->
		spl1-total (Dof x dof) (Dcomp x vx) (Dspl x x1 dspl : split1 (E x) (C x1 x) ([l] R l x x1)))
	<- ({x1} lang-indep neq-lang_01 (C x1) ([x] DeqC x x1))
	<- ({l} lang-indep2 neq-lang_02 neq-lang_12 (R l) ([x][x1] DeqR x x1 l ))
	<- ({x}{x1}{s} split1-resp (Dspl x x1 s) eq-term_ (DeqC x x1) (DeqR x x1) (Dspl' x x1 s)).


%% MAIN PROOF OF TOTALITY
- : spl1-total (of^-tup Dof1 Dof2) (form^tup-c Dcomp1 Dcomp2) (spl1-tup Dspl1 Dspl2)
	<- spl1-total Dof1 Dcomp1 Dspl1
	<- spl1-total Dof2 Dcomp2 Dspl2.
- : spl1-total (of^-pi1 Dof) (form^pi1-c Dcomp) (spl1-pi1 Dspl)
	<- spl1-total Dof Dcomp Dspl.
- : spl1-total (of^-pi2 Dof) (form^pi2-c Dcomp) (spl1-pi2 Dspl)
	<- spl1-total Dof Dcomp Dspl.
- : spl1-total (of^-roll _ Dof) (form^roll-c Dcomp) (spl1-roll Dspl)
	<- spl1-total Dof Dcomp Dspl.
- : spl1-total (of^-unrl Dof) (form^unrl-c Dcomp) (spl1-unrl Dspl)
	<- spl1-total Dof Dcomp Dspl.
- : spl1-total (of^-app Dof1 Dof2) (form^app-c Dcomp1 Dcomp2) (spl1-app Dspl1 Dspl2)
	<- spl1-total Dof1 Dcomp1 Dspl1
	<- spl1-total Dof2 Dcomp2 Dspl2.
- : spl1-total (of^-next Dof) (form^next-c Dcomp) (spl1-next Dspl)
	<- spl2-total Dof Dcomp Dspl.
- : spl1-total (of^-exv Dof) (form^exv Dcomp) (spl1-exv Dsplv)
	<- splv-total Dof Dcomp Dsplv.
- : spl1-total (of^-grnd Dof) (form^grnd Dcomp) (spl1-grnd Dspl)
	<- splg-total Dof Dcomp Dspl.
- : spl1-total (of^-letg DofE DofM) (form^letg DcompE DcompM) (spl1-letg DsplrE DsplM)
	<- spl1-total DofE DcompE DsplrE
	<- spl1-total/branchG DofM DcompM DsplM.
- : spl1-total (of^-caseg DofE DofM DofN) (form^caseg DcompE DcompM DcompN) (spl1-caseg DsplrE DsplM DsplN)
	<- spl1-total DofE DcompE DsplrE
	<- spl1-total/branchG DofM DcompM DsplM
	<- spl1-total/branchG DofN DcompN DsplN.

- : spl2-total (of^-tup Dof1 Dof2) (form^tup-c Dcomp1 Dcomp2) (spl2-tup Dspl1 Dspl2)
	<- spl2-total Dof1 Dcomp1 Dspl1
	<- spl2-total Dof2 Dcomp2 Dspl2.
- : spl2-total (of^-pi1 Dof) (form^pi1-c Dcomp) (spl2-pi1 Dspl)
	<- spl2-total Dof Dcomp Dspl.
- : spl2-total (of^-pi2 Dof) (form^pi2-c Dcomp) (spl2-pi2 Dspl)
	<- spl2-total Dof Dcomp Dspl.
- : spl2-total (of^-inj1 _ Dof _) (form^inj1-c Dcomp _) (spl2-inj1 Dspl)
	<- spl2-total Dof Dcomp Dspl.
- : spl2-total (of^-inj2 _ Dof _) (form^inj2-c Dcomp _) (spl2-inj2 Dspl)
	<- spl2-total Dof Dcomp Dspl.
- : spl2-total (of^-case DofE DofM DofN _) (form^case-c2 DcompE DcompM DcompN) (spl2-case DsplrE DsplM DsplN)
	<- spl2-total DofE DcompE DsplrE
	<- spl2-total/branch DofM DcompM DsplM
	<- spl2-total/branch DofN DcompN DsplN.
- : spl2-total (of^-roll _ Dof) (form^roll-c Dcomp) (spl2-roll Dspl)
	<- spl2-total Dof Dcomp Dspl.
- : spl2-total (of^-unrl Dof) (form^unrl-c Dcomp) (spl2-unrl Dspl)
	<- spl2-total Dof Dcomp Dspl.
- : spl2-total (of^-lam _ Dof) (form^lam2c Dcomp) (spl2-lam Dspl)
	<- spl2-total/branch Dof Dcomp Dspl.
- : spl2-total (of^-app Dof1 Dof2) (form^app-c Dcomp1 Dcomp2) (spl2-app Dspl1 Dspl2)
	<- spl2-total Dof1 Dcomp1 Dspl1
	<- spl2-total Dof2 Dcomp2 Dspl2.
- : spl2-total (of^-let DofE DofM) (form^let-c DcompE DcompM) (spl2-let DsplrE DsplM)
	<- spl2-total DofE DcompE DsplrE
	<- spl2-total/branch DofM DcompM DsplM.
- : spl2-total (of^-prev Dof) (form^prev Dcomp) (spl2-prev Dspl)
	<- spl1-total Dof Dcomp Dspl.
- : spl2-total (of^-exv Dof) (form^exv Dcomp) (spl2-exv Dsplr)
	<- splr-total Dof Dcomp Dsplr.

%worlds (total-block) 
	(spl1-total _ _ _) 
	(spl1-total/branchG _ _ _) 
	(spl2-total _ _ _) 
	(spl2-total/branch _ _ _) 
	(splv-total _ _ _).
%total (D1 D2 D3 D4 D5) 
	(spl1-total D1 _ _) 
	(spl1-total/branchG D2 _ _) 
	(spl2-total D3 _ _) 
	(spl2-total/branch D4 _ _) 
	(splv-total D5 _ _).